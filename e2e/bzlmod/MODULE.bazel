"""rules_pycross e2e tests"""

module(
    name = "rules_pycross_smoke",
    version = "0.0.0",
    compatibility_level = 1,
)

# Deps
bazel_dep(name = "aspect_bazel_lib", version = "2.3.0")
bazel_dep(name = "bazel_skylib", version = "1.4.2")
bazel_dep(name = "hermetic_cc_toolchain", version = "2.2.1")
bazel_dep(name = "platforms", version = "0.0.8")
bazel_dep(name = "rules_cc", version = "0.0.9")
bazel_dep(name = "rules_python", version = "0.31.0")
bazel_dep(name = "rules_pycross", version = "0.0.0")
local_path_override(
    module_name = "rules_pycross",
    path = "../..",
)

# CC toolchain

toolchains = use_extension("@hermetic_cc_toolchain//toolchain:ext.bzl", "toolchains")
use_repo(toolchains, "zig_sdk")

register_toolchains(
    "@zig_sdk//toolchain:linux_amd64_gnu.2.19",
    "@zig_sdk//toolchain:linux_arm64_gnu.2.28",
    "@zig_sdk//toolchain:darwin_amd64",
    "@zig_sdk//toolchain:darwin_arm64",
)

# Alternative CC toolchain (not registered, selected via --extra_toolchains in .bazelrc)

bazel_dep(name = "toolchains_llvm", version = "0.10.3")

# Configure and register the toolchain.
llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm")
llvm.toolchain(
    llvm_version = "16.0.0",
)
use_repo(llvm, "llvm_toolchain")
# use_repo(llvm, "llvm_toolchain_llvm") # if you depend on specific tools in scripts
#register_toolchains("@llvm_toolchain//:all")

# Python

python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.toolchain(python_version = "3.10.11")

# The default is latest - 1 to make sure nothing assumes latest == default
python.toolchain(
    is_default = True,
    python_version = "3.11.6",
)
python.toolchain(python_version = "3.12.0")

# Third-party deps

zstd = use_extension(":zstd.bzl", "zstd")
use_repo(zstd, "zstd")

# Pycross
environments = use_extension("@rules_pycross//pycross/extensions:environments.bzl", "environments")
environments.create_for_python_toolchains(
    name = "smoke_environments",
    platforms = [
        "aarch64-apple-darwin",
        "aarch64-unknown-linux-gnu",
        "x86_64-unknown-linux-gnu",
    ],
    python_versions = [
        "3.10.11",
        "3.11.6",
        "3.12.0",
        "3.12",
    ],
)
use_repo(environments, "smoke_environments")

# Use the lock_import extension to import external lock files.
lock_import = use_extension("@rules_pycross//pycross/extensions:lock_import.bzl", "lock_import")

# simple lock_repo with Poetry
lock_import.import_poetry(
    default_alias_single_version = True,
    lock_file = "//:poetry.lock",
    project_file = "//:pyproject.toml",
    repo = "poetry",
    target_environments = ["@smoke_environments//:environments"],
)

# lock_repo with PDM and some package overrides
lock_import.import_pdm(
    default_alias_single_version = True,
    default_build_dependencies = [
        "setuptools",
        "wheel",
    ],
    local_wheels = [
        "//:cowsay-6.1-py3-none-any.whl",
    ],
    lock_file = "//:pdm.lock",
    project_file = "//:pyproject.toml",
    repo = "pdm",
    target_environments = ["@smoke_environments//:environments"],
)

# lock_repo with UV and some package overrides
lock_import.import_uv(
    default_alias_single_version = True,
    default_build_dependencies = [
        "setuptools",
        "wheel",
    ],
    local_wheels = [
        "//:cowsay-6.1-py3-none-any.whl",
    ],
    lock_file = "//:uv.lock",
    project_file = "//:pyproject.toml",
    repo = "uv",
    target_environments = ["@smoke_environments//:environments"],
)

# lock_repo with PDM and some package overrides
lock_import.package(
    name = "regex",
    always_build = True,
    build_dependencies = [
        "setuptools",
        "wheel",
    ],
    repo = "pdm",
)
lock_import.package(
    name = "zstandard",
    always_build = True,
    build_target = "@//pdm:zstandard_build",
    repo = "pdm",
)

# The actual repos are loaded from the lock_repos extension.
lock_repos = use_extension("@rules_pycross//pycross/extensions:lock_repos.bzl", "lock_repos")
use_repo(lock_repos, "pdm", "poetry", "uv")

# Lock repos for vended lock files
lock_file = use_extension("@rules_pycross//pycross/extensions:lock_file.bzl", "lock_file")
lock_file.instantiate(
    name = "poetry_lock_file_repo",
    lock_file = "//lock_file:poetry_lock.bzl",
)
lock_file.instantiate(
    name = "pdm_lock_file_repo",
    lock_file = "//lock_file:pdm_lock.bzl",
)
use_repo(
    lock_file,
    "pdm_lock_file_repo",
    "poetry_lock_file_repo",
)
