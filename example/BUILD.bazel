load("@bazel_skylib//rules:diff_test.bzl", "diff_test")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@jvolkman_rules_pycross//pycross:defs.bzl", "pycross_target_environment", "pycross_lock_file")

config_setting(
    name = "darwin_x86",
    constraint_values = [
        "@platforms//os:macos",
        "@platforms//cpu:x86_64",
    ],
)

config_setting(
    name = "linux_x86",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

pycross_target_environment(
    name = "python_darwin_x86_64",
    python_compatible_with = [
        "@platforms//os:macos",
        "@platforms//cpu:x86_64",
    ],
    version = "3.9.12",
    abis = ["cp39"],
    platforms = ["macosx_12_0_x86_64"],
)

pycross_target_environment(
    name = "python_darwin_arm64",
    python_compatible_with = [
        "@platforms//os:macos",
        "@platforms//cpu:arm64",
    ],
    version = "3.9.12",
    abis = ["cp39"],
    platforms = ["macosx_12_0_arm64"],
)

pycross_target_environment(
    name = "python_linux_x86_64",
    python_compatible_with = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
    version = "3.9.12",
    abis = ["cp39"],
    platforms = ["manylinux2014_x86_64"] + [
        "manylinux_2_%s_x86_64" % str(i) for i in range(17, 25)
    ],
)

pycross_lock_file(
    name = "example_lock",
    poetry_project_file = "pyproject.toml",
    poetry_lock_file = "poetry.lock",
    target_environments = [
        ":python_darwin_x86_64",
        ":python_darwin_arm64",
        ":python_linux_x86_64",
    ],
    file_url_overrides = {
        "xmltodict-0.12.0-py2.py3-none-any.whl": "https://files.pythonhosted.org/packages/3.7/x/xmltodict/xmltodict-0.12.0-py2.py3-none-any.whl",
    },
    out = "example_lock.bzl",
)

_GENERATED = {
    "example_lock.bzl": "//:example_lock.bzl",
}

# Generate the updater script so there's only one target for devs to run,
# even if many generated files are in the source folder.
write_file(
    name = "gen_update",
    out = "update.sh",
    content = [
        # This depends on bash, would need tweaks for Windows
        "#!/usr/bin/env bash",
        # Bazel gives us a way to access the source folder!
        "cd $BUILD_WORKSPACE_DIRECTORY",
    ] + [
        # Paths are now relative to the workspace.
        # We can copy files from bazel-bin to the sources
        "cp -fv bazel-bin/{1} {0}".format(
            k,
            # Convert label to path
            v.replace(":", "/"),
        )
        for [k, v] in _GENERATED.items()
    ],
)

# This is what you can `bazel run` and it can write to the source folder
sh_binary(
    name = "update_example_lock",
    srcs = ["update.sh"],
    data = _GENERATED.values(),
)
