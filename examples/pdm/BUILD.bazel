load("@bazel_skylib//rules:write_file.bzl", "write_file")
load(
    "@rules_pycross//pycross:defs.bzl",
    "package_annotation",
    "pycross_lock_file",
    "pycross_pdm_lock_model",
    "pycross_target_environment",
)

_darwin_x86_64 = [
    "@platforms//os:macos",
    "@platforms//cpu:x86_64",
]

_darwin_arm64 = [
    "@platforms//os:macos",
    "@platforms//cpu:arm64",
]

_linux_x86_64 = [
    "@platforms//os:linux",
    "@platforms//cpu:x86_64",
]

platform(
    name = "darwin_x86_64",
    constraint_values = _darwin_x86_64,
)

platform(
    name = "darwin_arm64",
    constraint_values = _darwin_arm64,
)

platform(
    name = "linux_x86_64",
    constraint_values = _linux_x86_64,
)

pycross_target_environment(
    name = "python_darwin_x86_64",
    abis = ["cp311"],
    platforms = ["macosx_12_0_x86_64"],
    python_compatible_with = _darwin_x86_64,
    version = "3.11.6",
    visibility = ["//visibility:public"],
)

pycross_target_environment(
    name = "python_darwin_arm64",
    abis = ["cp311"],
    platforms = ["macosx_12_0_arm64"],
    python_compatible_with = _darwin_arm64,
    version = "3.11.6",
    visibility = ["//visibility:public"],
)

pycross_target_environment(
    name = "python_linux_x86_64",
    abis = ["cp311"],
    platforms = [
        "linux_x86_64",
        "manylinux2014_x86_64",
    ] + [
        "manylinux_2_%s_x86_64" % str(i)
        for i in range(17, 25)
    ],
    python_compatible_with = _linux_x86_64,
    version = "3.11.6",
    visibility = ["//visibility:public"],
)

pycross_pdm_lock_model(
    name = "example_lock_model",
    development_groups = [
        "dev",
    ],
    lock_file = "pdm.lock",
    optional_groups = [
        "cow",
    ],
    project_file = "pyproject.toml",
)

pycross_lock_file(
    name = "example_lock",
    out = "example_lock.bzl",
    annotations = {
        "cowsay": package_annotation(
            build_dependencies = [
                "setuptools",
                "wheel",
            ],
        ),
        "django-allauth": package_annotation(
            build_dependencies = [
                "setuptools",
                "wheel",
            ],
        ),
        "future@0.18.3": package_annotation(
            build_target = "@//deps:overridden_future_0.18.3",
        ),
        "greenlet": package_annotation(
            build_dependencies = [
                "setuptools",
                "wheel",
            ],
        ),
        "numpy": package_annotation(
            build_dependencies = [
                "cython",
                "setuptools",
                "wheel",
            ],
        ),
        "pbr@6.0.0": package_annotation(
            always_build = True,
            build_dependencies = [
                "setuptools",
                "wheel",
            ],
        ),
        "setproctitle": package_annotation(
            always_build = True,
            build_dependencies = [
                "setuptools",
                "wheel",
            ],
        ),
        "sphinxcontrib-applehelp": package_annotation(
            ignore_dependencies = [
                "sphinx",
            ],
        ),
        "sphinxcontrib-devhelp": package_annotation(
            ignore_dependencies = [
                "sphinx",
            ],
        ),
        "sphinxcontrib-htmlhelp": package_annotation(
            ignore_dependencies = [
                "sphinx",
            ],
        ),
        "sphinxcontrib-qthelp": package_annotation(
            ignore_dependencies = [
                "sphinx",
            ],
        ),
        "sphinxcontrib-serializinghtml": package_annotation(
            ignore_dependencies = [
                "sphinx",
            ],
        ),
        "sqlalchemy": package_annotation(
            build_dependencies = [
                "setuptools",
                "wheel",
            ],
        ),
        "tree-sitter": package_annotation(
            build_dependencies = [
                "setuptools",
                "wheel",
            ],
        ),
    },
    default_alias_single_version = True,
    local_wheels = [
        "//wheels",
    ],
    lock_model_file = ":example_lock_model",
    pypi_index = "https://pypi.org",
    remote_wheels = {
        "https://files.pythonhosted.org/packages/3.7/x/xmltodict/xmltodict-0.12.0-py2.py3-none-any.whl": "8bbcb45cc982f48b2ca8fe7e7827c5d792f217ecf1792626f808bf41c3b86051",
    },
    target_environments = [
        ":python_darwin_x86_64",
        ":python_darwin_arm64",
        ":python_linux_x86_64",
    ],
)

_GENERATED = {
    "example_lock.bzl": "//:example_lock.bzl",
}

# Generate the updater script so there's only one target for devs to run,
# even if many generated files are in the source folder.
write_file(
    name = "gen_update",
    out = "update.sh",
    content = [
        # This depends on bash, would need tweaks for Windows
        "#!/usr/bin/env bash",
        # Bazel gives us a way to access the source folder!
        "cd $BUILD_WORKSPACE_DIRECTORY",
    ] + [
        # Paths are now relative to the workspace.
        # We can copy files from bazel-bin to the sources
        "cp -fv bazel-bin/{1} {0}".format(
            k,
            # Convert label to path
            v.replace(":", "/"),
        )
        for [
            k,
            v,
        ] in _GENERATED.items()
    ],
)

# This is what you can `bazel run` and it can write to the source folder
sh_binary(
    name = "update_example_lock",
    srcs = ["update.sh"],
    data = _GENERATED.values(),
)
